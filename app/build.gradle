apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.autohome.common.gradlelearn"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs{
        release{
            storeFile file("../ylq.jks")
//            storePassword ""
            keyAlias "ylqkey"
//            keyPassword ""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "API_URL", "\"http://www.autohome.debug\"")
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "API_URL", "\"http://www.autohome.release\"")
            signingConfig signingConfigs.release
        }

        staging.initWith(buildTypes.debug)
        staging {
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            buildConfigField("String", "API_URL", "\"http://www.autohome.staging\"")
        }
    }

    //设置 flavor维度，及组合使用，顺序决定了组合后资源的优先级
//    flavorDimensions("color", "price")
    productFlavors {

        red {
            dimension "color"
//            applicationIdSuffix ".red"
            resValue("color", "flavor_color", "#ff0000")
        }

        blue {
            dimension "color"
//            applicationIdSuffix ".blue"
            resValue("color", "flavor_color", "#0000ff")
        }

        free {
            dimension "price"
//            applicationIdSuffix ".free"
        }

        paid {
            dimension "price"
//            applicationIdSuffix ".paid"
        }


    }


}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile project(':javalib')
    compile project(':androidlibrary')
    compile 'com.android.support:appcompat-v7:26.+'
//    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    testCompile 'junit:junit:4.12'
    stagingCompile 'com.google.code.gson:gson:2.2.4'
//    compile project(path: ':backend', configuration: 'android-endpoints')
}

task('hello') << {
    println('hello execution')
}

hello {
    println('hello configuration')
}

tasks.create(name : 'hello2') {
    doFirst {
        println('hello2 execution, not real fist')
    }

    doFirst {
        println('hello2 execution, do first')
    }

    doLast {
        println('hello2 execution, do last')
    }
}
//hello2.mustRunAfter(hello)
hello2.dependsOn hello

//动态获取签名密码
task getReleasePassword << {
    def password = ''
    if(rootProject.file('private.properties').exists()){
        java.util.Properties properties = new Properties()
        properties.load(rootProject.file('private.properties').newDataInputStream())
        password = properties.get('release.password')
    }

    if(!password?.trim()){
        password = new String(java.lang.System.console().readPassword("\nWhat is the secret password?"))
    }

    android.signingConfigs.release.storePassword = password;
    android.signingConfigs.release.keyPassword = password;
}

tasks.whenTaskAdded { theTask ->
    if(theTask.name == 'packageRedRelease' || theTask.name == 'packageBlueRelease'
            || theTask.name == 'packageFreeRelease' || theTask.name == 'packagePaidRelease'){
        theTask.dependsOn "getReleasePassword"
    }
}

//尝试过滤含blue 和 release的variant
android.variantFilter { variant ->
    if (variant.buildType.name.equals('release')) {
        variant.getFlavors().each() { flavor ->
            if (flavor.name.equals('blue')) {
                variant.setIgnore(true);
            }

        }
    }
}

android.applicationVariants.all { var ->
    //修改最终生成的apk文件名
    var.outputs.each { output ->
        def file = output.outputFile
        println(file.name)
        output.outputFile = new File(file.parent, file.name.replace(".apk", "-${var.versionName}.apk"))
    }

    //自定义task，执行安装并启动MainActivity
    if(var.install){
        println("generate runLauncher activity task for variant---->" + var.name)
        def newTaskName = "run${var.name.capitalize()}"
//        if(!tasks.findByName(newTaskName)){
            tasks.create(name:newTaskName, dependsOn: var.install) {
                description "Installs the ${var.description} and runs the main launcher activity"

                doFirst {
                    def classpath = var.applicationId
                    if(var.buildType.applicationIdSuffix){
                        classpath -= "${var.buildType.applicationIdSuffix}"
                    }

                    def lanchClass = "${var.applicationId}/${classpath}.MainActivity"
                    println('lancher activity --->' + lanchClass)
                    exec {
                        executable 'adb'
                        args = ['shell', 'am', 'start', '-n', lanchClass]
                    }
                }
            }

//        }
    }else{
        println("not generate runLauncher activity task for variant---->" + var.name)
    }
}




